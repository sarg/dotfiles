set positional-arguments

lock := "0"
guix-lock := if lock != "0" { "guix time-machine -C channels.lock --" } else { "guix" }

default:
    @just --list

l *args:
    @just -f {{source_file()}} {{args}} -L ~/devel/dotfiles/guix/personal

bump *args='':
    guix refresh -m bump-manifest.scm -L ~/devel/dotfiles/guix/personal {{args}}

refresh *args='':
    guix $0 {{args}}

import *args='':
    guix $0 {{args}}

style *args='':
    guix $0 {{args}}

shell *args='':
    guix $0 {{args}}

size *args='':
    guix $0 {{args}}

build *args='':
    guix $0 {{args}}

pull *args='':
    guix $0 {{args}}

update-channels:
    guix describe --format=channels > channels.lock

system *args='':
    direnv exec . sudo -E {{ guix-lock }} system reconfigure system.scm {{args}}

home *args='':
    {{ guix-lock }} home reconfigure home.scm {{args}}

gc sys='no':
    guix home delete-generations
    if [[ '{{sys}}' != 'no' ]]; then sudo guix system delete-generations; fi
    guix pull --delete-generations
    guix gc
